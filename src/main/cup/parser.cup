package lyc.compiler;

import java_cup.runtime.*;
import lyc.compiler.files.SymbolTableGenerator;

class Parser;


// Terminals

terminal ASSIG;
terminal PLUS;
terminal SUB;
terminal MULT;
terminal DIV;

terminal GREATER;
terminal LESS;
terminal EQUAL;

terminal AND;
terminal OR;
terminal NOT;

terminal OPEN_BRACKET;
terminal CLOSE_BRACKET;
terminal OPEN_CURLY_BRACKET;
terminal CLOSE_CURLY_BRACKET;
terminal OPEN_SQUARE_BRACKET;
terminal CLOSE_SQUARE_BRACKET;
terminal COLON;
terminal COMMA;

terminal INIT;
terminal READ;
terminal WRITE;

terminal WHILE;
terminal IF;
terminal ELSE;

terminal IGUALES;
terminal DO;
terminal CASE;
terminal DEFAULT;
terminal ENDDO;

terminal INT;
terminal FLOAT;
terminal STRING;

terminal INTEGER_CONSTANT;
terminal FLOAT_CONSTANT;
terminal STRING_CONSTANT;
terminal IDENTIFIER;

// Non Terminals
non terminal program;
non terminal sentence;
non terminal grammar;
non terminal declaration;
non terminal assignment;
non terminal expression;
non terminal term;
non terminal factor;
non terminal statement_block;
non terminal statement_list;
non terminal data_type;
non terminal in;
non terminal out;
non terminal if;
non terminal while;
non terminal condition;
non terminal logic_operator;
non terminal comparation;
non terminal case;
non terminal block_case;
non terminal statement_case;
non terminal default;
non terminal iguales;
non terminal lista_expression;

// Start Symbol Definition
start with program;

program ::=                                                                     {: System.out.println("Programa vacio"); :};
program ::= sentence                                                            {: System.out.println("Program"); :};
sentence ::= sentence grammar                                                   ;
sentence ::= grammar                                                            ;

grammar ::= statement_block                                                     ;
grammar ::= assignment                                                          ;
grammar ::= in                                                                  ;
grammar ::= out                                                                 ;
grammar ::= if                                                                  ;
grammar ::= while                                                               ;
grammar ::= case                                                                ;
grammar ::= iguales                                                             ;

statement_block ::= INIT OPEN_CURLY_BRACKET statement_list CLOSE_CURLY_BRACKET  {: System.out.println("Statement Block"); :};
statement_list ::= statement_list declaration                                   {: System.out.println(); :};
statement_list ::= declaration                                                  {: System.out.println(); :};
declaration ::= IDENTIFIER:id COMMA declaration                                 {: System.out.print(" "+id); :};
declaration ::= IDENTIFIER:id COLON data_type                                   {: System.out.print(" "+id); :};
data_type ::= INT                                                               {: System.out.print("DATA TYPE: INT -->");:};
data_type ::= FLOAT                                                             {: System.out.print("DATA TYPE: FLOAT -->");:};
data_type ::= STRING                                                            {: System.out.print("DATA TYPE: STRING -->");:};

assignment ::= IDENTIFIER:id ASSIG expression                                   {: System.out.println(id+" <-- Assignment Expression");
                                                                                   SymbolTableGenerator.getInstance().addTokenIdAssignment(id+""); :};
expression ::= expression PLUS term                                             {: System.out.println("+"); :};
expression ::= expression SUB term                                              {: System.out.println("-"); :};
expression ::= term                                                             {: System.out.println("Expression = Term"); :};
term ::= term MULT factor                                                       {: System.out.println("*"); :};
term ::= term DIV factor                                                        {: System.out.println("/"); :};
term ::= factor                                                                 {: System.out.println("Term = factor"); :};
factor ::= OPEN_BRACKET expression CLOSE_BRACKET                                {: System.out.println("Factor = (Expression)"); :};
factor ::= IDENTIFIER:id                                                        {: System.out.println("ID: " + id);
                                                                                   SymbolTableGenerator.getInstance().addTokenIdAssignment(id+""); :};
factor ::= INTEGER_CONSTANT:constant                                            {: System.out.println("CONSTANT INT: " + constant );
                                                                                   SymbolTableGenerator.getInstance().addTokenCteAssignment(constant+"", "Int"); :};
factor ::= FLOAT_CONSTANT:constant                                              {: System.out.println("CONSTANT FLOAT: " + constant );
                                                                                   SymbolTableGenerator.getInstance().addTokenCteAssignment(constant+"", "Float"); :};
factor ::= STRING_CONSTANT:constant                                             {: System.out.println("CONSTANT STRING: " + constant );
                                                                                   SymbolTableGenerator.getInstance().addTokenCteAssignment(constant+"", "String"); :};

in ::= READ OPEN_BRACKET IDENTIFIER:id CLOSE_BRACKET                            {: System.out.println("ID: "+ id + " <-- Read"); :};

out ::= WRITE OPEN_BRACKET IDENTIFIER:id CLOSE_BRACKET                          {: System.out.println("ID: "+ id + " <-- Write"); :};
out ::= WRITE OPEN_BRACKET STRING_CONSTANT:constant CLOSE_BRACKET               {: System.out.println("CONSTANT STRING: "+ constant + " <-- Write"); :};

if ::= IF condition OPEN_CURLY_BRACKET sentence CLOSE_CURLY_BRACKET                                                             {: System.out.println("If Block"); :};
if ::= IF condition OPEN_CURLY_BRACKET sentence CLOSE_CURLY_BRACKET ELSE OPEN_CURLY_BRACKET sentence CLOSE_CURLY_BRACKET        {: System.out.println("If with Else Block"); :};

while ::= WHILE condition OPEN_CURLY_BRACKET sentence CLOSE_CURLY_BRACKET                                                       {: System.out.println("While Block"); :};

condition ::= OPEN_BRACKET expression comparation expression CLOSE_BRACKET                                                      {: System.out.println("Simple Condition"); :};
condition ::= OPEN_BRACKET NOT expression comparation expression CLOSE_BRACKET                                                  {: System.out.println("Not Condition"); :};
condition ::= OPEN_BRACKET expression comparation expression logic_operator expression comparation expression CLOSE_BRACKET     {: System.out.println("Multiple Condition"); :};
logic_operator ::= AND                                                          {: System.out.println("& (AND)"); :};
logic_operator ::= OR                                                           {: System.out.println("|| (OR)"); :};
comparation ::= LESS                                                            {: System.out.println("< (LESS)"); :};
comparation ::= GREATER                                                         {: System.out.println("> (GREATER)"); :};
comparation ::= EQUAL                                                           {: System.out.println("== (EQUAL)"); :};

case ::= DO IDENTIFIER:id block_case ENDDO                                      {: System.out.println("Block Case --> "+id); :};
block_case ::= statement_case;
block_case ::= statement_case default;
statement_case ::= CASE IDENTIFIER comparation expression OPEN_CURLY_BRACKET sentence CLOSE_CURLY_BRACKET                ;
statement_case ::= statement_case CASE IDENTIFIER comparation expression OPEN_CURLY_BRACKET sentence CLOSE_CURLY_BRACKET ;
default ::= DEFAULT OPEN_CURLY_BRACKET sentence CLOSE_CURLY_BRACKET             {: System.out.println("Default Case"); :};

iguales ::= IGUALES OPEN_BRACKET expression COMMA OPEN_SQUARE_BRACKET lista_expression CLOSE_SQUARE_BRACKET CLOSE_BRACKET  {: System.out.println("Iguales Block"); :};
lista_expression ::= lista_expression COMMA expression;
lista_expression ::= expression;